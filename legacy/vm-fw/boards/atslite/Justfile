output_dir := "build"
net_core := "net-core"

# Default recipe - show available commands
default:
    @just --list

# Clean build artifacts
clean:
    rm -rf {{output_dir}} target bootloader/target
    cargo clean
    cd bootloader && cargo clean

# Build ATSlite application for bank A (default)
build-app:
    cargo build --release --target thumbv8m.main-none-eabihf

# Build ATSlite application for bank A
build-app-a:
    APP_BANK=bank_a cargo build --release --target thumbv8m.main-none-eabihf --target-dir target-bank-a

# Build ATSlite application for bank B
build-app-b:
    APP_BANK=bank_b cargo build --release --target thumbv8m.main-none-eabihf --target-dir target-bank-b

# Build ATSlite stage1 bootloader
build-stage1:
    cd stage1-bootloader && cargo build --release --target thumbv8m.main-none-eabihf

# Build ATSlite stage2 bootloader (DFU)
build-bootloader:
    cd bootloader && cargo build --release --target thumbv8m.main-none-eabihf --features defmt

# Build ATSlite net-core
build-net:
    cd {{net_core}} && cargo build --release

# Build ATSlite netcore bootloader
build-netcore-bootloader:
    cd netcore-bootloader && cargo build --release --target thumbv8m.main-none-eabi

# Build complete ATSlite firmware with both banks
build: build-stage1 build-app-a build-app-b build-bootloader build-netcore-bootloader build-net
    mkdir -p {{output_dir}}
    cp stage1-bootloader/target/thumbv8m.main-none-eabihf/release/atslite-stage1-bootloader {{output_dir}}/stage1-bootloader.elf
    cp target-bank-a/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-a.elf
    cp target-bank-b/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-b.elf
    cp bootloader/target/thumbv8m.main-none-eabihf/release/atslite-bootloader {{output_dir}}/stage2-bootloader.elf
    cp netcore-bootloader/target/thumbv8m.main-none-eabi/release/atslite-netcore-bootloader {{output_dir}}/netcore-bootloader.elf
    cp {{net_core}}/target/thumbv8m.main-none-eabi/release/net-core {{output_dir}}/net-core.elf
    llvm-objcopy -O ihex {{output_dir}}/stage1-bootloader.elf {{output_dir}}/stage1-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/app-a.elf {{output_dir}}/app-a.hex
    llvm-objcopy -O ihex {{output_dir}}/app-b.elf {{output_dir}}/app-b.hex
    llvm-objcopy -O ihex {{output_dir}}/stage2-bootloader.elf {{output_dir}}/stage2-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/netcore-bootloader.elf {{output_dir}}/netcore-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/net-core.elf {{output_dir}}/net-core.hex
    llvm-objcopy -O binary {{output_dir}}/app-a.elf {{output_dir}}/app-a.bin
    llvm-objcopy -O binary {{output_dir}}/app-b.elf {{output_dir}}/app-b.bin
    llvm-objcopy -O binary {{output_dir}}/stage2-bootloader.elf {{output_dir}}/stage2-bootloader.bin
    llvm-objcopy -O binary {{output_dir}}/net-core.elf {{output_dir}}/net-core.bin
    mergehex -o {{output_dir}}/merged.hex -m {{output_dir}}/stage1-bootloader.hex {{output_dir}}/stage2-bootloader.hex {{output_dir}}/app-a.hex {{output_dir}}/app-b.hex {{output_dir}}/netcore-bootloader.hex {{output_dir}}/net-core.hex

# Build complete dual-bank firmware with both app banks and DFU binaries
build-dual-bank: build-stage1 build-app-a build-app-b build-bootloader build-net
    mkdir -p {{output_dir}}
    cp stage1-bootloader/target/thumbv8m.main-none-eabihf/release/atslite-stage1-bootloader {{output_dir}}/stage1-bootloader.elf
    cp target-bank-a/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-a.elf
    cp target-bank-b/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-b.elf
    cp bootloader/target/thumbv8m.main-none-eabihf/release/atslite-bootloader {{output_dir}}/stage2-bootloader.elf
    cp {{net_core}}/target/thumbv8m.main-none-eabi/release/net-core {{output_dir}}/net-core.elf
    llvm-objcopy -O ihex {{output_dir}}/stage1-bootloader.elf {{output_dir}}/stage1-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/app-a.elf {{output_dir}}/app-a.hex
    llvm-objcopy -O ihex {{output_dir}}/app-b.elf {{output_dir}}/app-b.hex
    llvm-objcopy -O ihex {{output_dir}}/stage2-bootloader.elf {{output_dir}}/stage2-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/net-core.elf {{output_dir}}/net-core.hex
    llvm-objcopy -O binary {{output_dir}}/app-a.elf {{output_dir}}/app-a.bin
    llvm-objcopy -O binary {{output_dir}}/app-b.elf {{output_dir}}/app-b.bin
    llvm-objcopy -O binary {{output_dir}}/net-core.elf {{output_dir}}/net-core.bin
    mergehex -o {{output_dir}}/merged-dual.hex -m {{output_dir}}/stage1-bootloader.hex {{output_dir}}/stage2-bootloader.hex {{output_dir}}/app-a.hex {{output_dir}}/app-b.hex {{output_dir}}/net-core.hex

# Flash ATSlite firmware
flash: build
    nrfjprog --program {{output_dir}}/merged.hex --sectorerase --verify --reset
    # Clear bootloader state pages so boot state starts erased (0xFFFFFFFF)
    nrfjprog --erasepage 0xF9000
    nrfjprog --erasepage 0xFA000

# Flash with chip erase
flash-chiperase: build
    nrfjprog --program {{output_dir}}/merged.hex --chiperase --verify --reset
    nrfjprog --erasepage 0xFC000
    nrfjprog --erasepage 0xFD000

# Flash with recover
flash-recover: build
    nrfjprog --program {{output_dir}}/merged.hex --recover --verify --reset
    nrfjprog --erasepage 0xFC000
    nrfjprog --erasepage 0xFD000

# Flash dual-bank with chip erase
flash-dual-chiperase: build-dual-bank
    nrfjprog --program {{output_dir}}/merged-dual.hex --chiperase --verify --reset
    nrfjprog --erasepage 0xFC000
    nrfjprog --erasepage 0xFD000

# Flash dual-bank with recover
flash-dual-recover: build-dual-bank
    nrfjprog --program {{output_dir}}/merged-dual.hex --recover --verify --reset
    nrfjprog --erasepage 0xFC000
    nrfjprog --erasepage 0xFD000

# Flash application only
flash-app: build-app
    mkdir -p {{output_dir}}
    cp target/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app.elf
    llvm-objcopy -O ihex {{output_dir}}/app.elf {{output_dir}}/app.hex
    nrfjprog --program {{output_dir}}/app.hex --sectorerase --verify --reset

# Attach probe-rs to app core
attach-app-a:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/app-a.elf

attach-app-b:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/app-b.elf

# Attach probe-rs to network core
attach-net:
    probe-rs attach --chip nRF5340_xxAA --rtt-scan-memory {{output_dir}}/net-core.elf

# Attach to app core bootloader (net core unlocked for debugging)
attach-bootloader:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/stage2-bootloader.elf

# Attach probe-rs to network core bootloader
attach-net-bootloader:
    probe-rs attach --chip nRF5340_xxAA --rtt-scan-memory netcore-bootloader/target/thumbv8m.main-none-eabi/release/atslite-netcore-bootloader

# Reset device
reset:
    nrfjprog --reset

# Generate a release entry in manifests/releases.alpha.json from current build
gen-release version:
    #!/usr/bin/env bash
    set -euo pipefail

    MANIFEST="../../../../manifests/releases.alpha.json"
    BUILD_DIR="{{output_dir}}"

    # Check that build files exist
    for f in app-a.bin app-b.bin net-core.bin stage2-bootloader.bin; do
        if [ ! -f "$BUILD_DIR/$f" ]; then
            echo "Error: $BUILD_DIR/$f not found. Run 'just build' first."
            exit 1
        fi
    done

    # Compute sha256 and size for each file
    APP_A_SHA=$(sha256sum "$BUILD_DIR/app-a.bin" | cut -d' ' -f1)
    APP_A_SIZE=$(stat -c%s "$BUILD_DIR/app-a.bin" 2>/dev/null || stat -f%z "$BUILD_DIR/app-a.bin")

    APP_B_SHA=$(sha256sum "$BUILD_DIR/app-b.bin" | cut -d' ' -f1)
    APP_B_SIZE=$(stat -c%s "$BUILD_DIR/app-b.bin" 2>/dev/null || stat -f%z "$BUILD_DIR/app-b.bin")

    NET_CORE_SHA=$(sha256sum "$BUILD_DIR/net-core.bin" | cut -d' ' -f1)
    NET_CORE_SIZE=$(stat -c%s "$BUILD_DIR/net-core.bin" 2>/dev/null || stat -f%z "$BUILD_DIR/net-core.bin")

    BOOTLOADER_SHA=$(sha256sum "$BUILD_DIR/stage2-bootloader.bin" | cut -d' ' -f1)
    BOOTLOADER_SIZE=$(stat -c%s "$BUILD_DIR/stage2-bootloader.bin" 2>/dev/null || stat -f%z "$BUILD_DIR/stage2-bootloader.bin")

    TODAY=$(date +%Y-%m-%d)

    # Create the new firmware entry JSON
    NEW_ENTRY=$(cat <<EOF
    {
      "version": "{{version}}",
      "files": {
        "app-a": {
          "sha256": "$APP_A_SHA",
          "size": $APP_A_SIZE
        },
        "app-b": {
          "sha256": "$APP_B_SHA",
          "size": $APP_B_SIZE
        },
        "net-core": {
          "sha256": "$NET_CORE_SHA",
          "size": $NET_CORE_SIZE
        },
        "stage2-bootloader": {
          "sha256": "$BOOTLOADER_SHA",
          "size": $BOOTLOADER_SIZE
        }
      },
      "released": "$TODAY"
    }
    EOF
    )

    # Use jq to prepend the new entry to the firmwares array
    jq --argjson entry "$NEW_ENTRY" '.devices["legacy-atslite1"].firmwares = [$entry] + .devices["legacy-atslite1"].firmwares' "$MANIFEST" > "$MANIFEST.tmp"
    mv "$MANIFEST.tmp" "$MANIFEST"

    echo "Added release {{version}} to $MANIFEST"
    echo "  app-a: $APP_A_SHA ($APP_A_SIZE bytes)"
    echo "  app-b: $APP_B_SHA ($APP_B_SIZE bytes)"
    echo "  net-core: $NET_CORE_SHA ($NET_CORE_SIZE bytes)"
    echo "  stage2-bootloader: $BOOTLOADER_SHA ($BOOTLOADER_SIZE bytes)"
