output_dir := "build"
net_core := "net-core"

# Default recipe - show available commands
default:
    @just --list

# Clean build artifacts
clean:
    rm -rf {{output_dir}} target bootloader/target
    cargo clean
    cd bootloader && cargo clean

# Build ATSlite application for bank A (default)
build-app:
    cargo build --release --target thumbv8m.main-none-eabihf

# Build ATSlite application for bank A
build-app-a:
    APP_BANK=bank_a cargo build --release --target thumbv8m.main-none-eabihf --target-dir target-bank-a

# Build ATSlite application for bank B
build-app-b:
    APP_BANK=bank_b cargo build --release --target thumbv8m.main-none-eabihf --target-dir target-bank-b

# Build ATSlite stage1 bootloader
build-stage1:
    cd stage1-bootloader && cargo build --release --target thumbv8m.main-none-eabihf

# Build ATSlite stage2 bootloader (DFU)
build-bootloader:
    cd bootloader && cargo build --release --target thumbv8m.main-none-eabihf --features defmt

# Build ATSlite net-core
build-net:
    cd {{net_core}} && cargo build --release

# Build ATSlite netcore bootloader
build-netcore-bootloader:
    cd netcore-bootloader && cargo build --release --target thumbv8m.main-none-eabi

# Build complete ATSlite firmware (bank A only for compatibility)
build: build-stage1 build-app-a build-bootloader build-netcore-bootloader build-net
    mkdir -p {{output_dir}}
    cp stage1-bootloader/target/thumbv8m.main-none-eabihf/release/atslite-stage1-bootloader {{output_dir}}/stage1-bootloader.elf
    cp target-bank-a/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-a.elf
    cp bootloader/target/thumbv8m.main-none-eabihf/release/atslite-bootloader {{output_dir}}/stage2-bootloader.elf
    cp netcore-bootloader/target/thumbv8m.main-none-eabi/release/atslite-netcore-bootloader {{output_dir}}/netcore-bootloader.elf
    cp {{net_core}}/target/thumbv8m.main-none-eabi/release/net-core {{output_dir}}/net-core.elf
    llvm-objcopy -O ihex {{output_dir}}/stage1-bootloader.elf {{output_dir}}/stage1-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/app-a.elf {{output_dir}}/app-a.hex
    llvm-objcopy -O ihex {{output_dir}}/stage2-bootloader.elf {{output_dir}}/stage2-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/netcore-bootloader.elf {{output_dir}}/netcore-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/net-core.elf {{output_dir}}/net-core.hex
    llvm-objcopy -O binary {{output_dir}}/app-a.elf {{output_dir}}/app-a.bin
    llvm-objcopy -O binary {{output_dir}}/net-core.elf {{output_dir}}/net-core.bin
    llvm-objcopy -O binary {{output_dir}}/net-core.elf {{output_dir}}/net-core-dfu.bin
    mergehex -o {{output_dir}}/merged.hex -m {{output_dir}}/stage1-bootloader.hex {{output_dir}}/stage2-bootloader.hex {{output_dir}}/app-a.hex {{output_dir}}/netcore-bootloader.hex {{output_dir}}/net-core.hex

# Build complete dual-bank firmware with both app banks and DFU binaries
build-dual-bank: build-stage1 build-app-a build-app-b build-bootloader build-net
    mkdir -p {{output_dir}}
    cp stage1-bootloader/target/thumbv8m.main-none-eabihf/release/atslite-stage1-bootloader {{output_dir}}/stage1-bootloader.elf
    cp target-bank-a/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-a.elf
    cp target-bank-b/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app-b.elf
    cp bootloader/target/thumbv8m.main-none-eabihf/release/atslite-bootloader {{output_dir}}/stage2-bootloader.elf
    cp {{net_core}}/target/thumbv8m.main-none-eabi/release/net-core {{output_dir}}/net-core.elf
    llvm-objcopy -O ihex {{output_dir}}/stage1-bootloader.elf {{output_dir}}/stage1-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/app-a.elf {{output_dir}}/app-a.hex
    llvm-objcopy -O ihex {{output_dir}}/app-b.elf {{output_dir}}/app-b.hex
    llvm-objcopy -O ihex {{output_dir}}/stage2-bootloader.elf {{output_dir}}/stage2-bootloader.hex
    llvm-objcopy -O ihex {{output_dir}}/net-core.elf {{output_dir}}/net-core.hex
    llvm-objcopy -O binary {{output_dir}}/app-a.elf {{output_dir}}/app-a-dfu.bin
    llvm-objcopy -O binary {{output_dir}}/app-b.elf {{output_dir}}/app-b-dfu.bin
    llvm-objcopy -O binary {{output_dir}}/net-core.elf {{output_dir}}/net-core-dfu.bin
    mergehex -o {{output_dir}}/merged-dual.hex -m {{output_dir}}/stage1-bootloader.hex {{output_dir}}/stage2-bootloader.hex {{output_dir}}/app-a.hex {{output_dir}}/app-b.hex {{output_dir}}/net-core.hex

# Flash ATSlite firmware
flash: build
    nrfjprog --program {{output_dir}}/merged.hex --sectorerase --verify --reset
    # Clear bootloader state page so boot state starts erased (0xFFFFFFFF)
    nrfjprog --erasepage 0xC000

# Flash with chip erase
flash-chiperase: build
    nrfjprog --program {{output_dir}}/merged.hex --chiperase --verify --reset
    nrfjprog --erasepage 0xC000

# Flash with recover
flash-recover: build
    nrfjprog --program {{output_dir}}/merged.hex --recover --verify --reset
    nrfjprog --erasepage 0xC000

# Flash dual-bank with chip erase
flash-dual-chiperase: build-dual-bank
    nrfjprog --program {{output_dir}}/merged-dual.hex --chiperase --verify --reset
    nrfjprog --erasepage 0xC000

# Flash dual-bank with recover
flash-dual-recover: build-dual-bank
    nrfjprog --program {{output_dir}}/merged-dual.hex --recover --verify --reset
    nrfjprog --erasepage 0xC000

# Flash application only
flash-app: build-app
    mkdir -p {{output_dir}}
    cp target/thumbv8m.main-none-eabihf/release/app {{output_dir}}/app.elf
    llvm-objcopy -O ihex {{output_dir}}/app.elf {{output_dir}}/app.hex
    nrfjprog --program {{output_dir}}/app.hex --sectorerase --verify --reset

# Attach probe-rs to app core
attach-app-a:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/app-a.elf

attach-app-b:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/app-b.elf

# Attach probe-rs to network core
attach-net:
    probe-rs attach --chip nRF5340_xxAA --rtt-scan-memory {{output_dir}}/net-core.elf

# Attach to app core bootloader (net core unlocked for debugging)
attach-bootloader:
    probe-rs attach --chip nRF5340_xxAA {{output_dir}}/stage2-bootloader.elf

# Attach probe-rs to network core bootloader
attach-net-bootloader:
    probe-rs attach --chip nRF5340_xxAA --rtt-scan-memory netcore-bootloader/target/thumbv8m.main-none-eabi/release/atslite-netcore-bootloader

# Reset device
reset:
    nrfjprog --reset
